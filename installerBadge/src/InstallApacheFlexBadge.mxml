<?xml version="1.0" encoding="utf-8"?>
<!--

Licensed to the Apache Software Foundation (ASF) under one or more
contributor license agreements.  See the NOTICE file distributed with
this work for additional information regarding copyright ownership.
The ASF licenses this file to You under the Apache License, Version 2.0
(the "License"); you may not use this file except in compliance with
the License.  You may obtain a copy of the License at

http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.

-->

<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009" 
			   xmlns:s="library://ns.adobe.com/flex/spark" 
			   xmlns:mx="library://ns.adobe.com/flex/mx" 
			   xmlns:components="org.apache.flex.installApacheFlexBadge.components.*"
			   height="300"
			   width="300" 
			   creationComplete="creationCompleteHandler(event)">
	
	<fx:Style source="/assets/styles/ApacheFlexToolsStyle.css" />
	
	<fx:Script><![CDATA[
		
		import flash.net.navigateToURL;
		
		import flashx.textLayout.elements.TextFlow;
		
		import mx.controls.Alert;
		import mx.core.FlexGlobals;
		import mx.events.FlexEvent;
		
		import org.apache.flex.installApacheFlexBadge.resources.EmbeddedMedia;
		import org.apache.flex.installApacheFlexBadge.utils.InternetUtil;
		
		import spark.utils.TextFlowUtil;
		
		//----------------------------------------------------------------------
		//
		//    Constants
		//
		//----------------------------------------------------------------------
		
		private const AIR_SWF_URL:String = "http://airdownload.adobe.com/air/browserapi/air.swf";

		private const APPLICATION_EXTENSION_MAC:String = ".dmg";
		private const APPLICATION_EXTENSION_WIN:String = ".exe";
		
		private const ID_FILE_NODE:String = "ApacheFlexSDK";
		
		private const INFO_DISCLAIMER_XML:XML = 
			<TextFlow xmlns="http://ns.adobe.com/textLayout/2008" fontFamily="openSansSemiBold">
				<p>
					Apache Flex is an effort undergoing incubation at The Apache Software Foundation (ASF), sponsored by the Apache Incubator.
				</p>
				<br />
				<p>
					Incubation is required of all newly accepted projects until a further review indicates that the infrastructure, communications, and decision making process have stabilized in a manner consistent with other successful ASF projects.
				</p>
				<br />
				<p>
					While incubation status is not necessarily a reflection of the completeness or stability of the code, it does indicate that the project has yet to be fully endorsed by the ASF.
				</p>
			</TextFlow>;
		private const INFO_ERROR_OPERATING_SYSTEM:String = "The Apache Flex SDK Installer application\nis not supported on your operating system.";
		private const INFO_ERROR_RETRIEVE_CONFIG:String = "The application was unable to retrieve\nthe configuration file.";
		private const INFO_FOR_IDE:String = "for Adobe Flash Builder";
		
		private const LABEL_BACK:String = "Close";
		private const LABEL_DISCLAIMER:String = "( disclaimer )";
		private const LABEL_DOWNLOAD:String = "DOWNLOAD";
		
		private const PLATFORM_MAC:String = "Mac";
		private const PLATFORM_WIN:String = "Windows";
		
		private const TITLE_ERROR_INTERNET:String = "Internet Error";
		private const TITLE_ERROR_PLATFORM:String = "Platform Error";
		
		//----------------------------------------------------------------------
		//
		//    Variables
		//
		//----------------------------------------------------------------------
		
		private var _applicationExtension:String;
		
		private var _fileNamePrefix:String;
		
		private var _installerConfigFile:String;
		
		private var _installerDownloadURL:String;
		
		private var _installerFileName:String;
		
		private var _internetUtil:InternetUtil;
		
		[Bindable]
		private var _sdkVersionString:String = "";
		
		//----------------------------------------------------------------------
		//
		//    Methods
		//
		//----------------------------------------------------------------------
		
		//----------------------------------
		//    btnDownload_clickHandler
		//----------------------------------
		
		private function btnDownload_clickHandler(event:MouseEvent):void
		{
			btnDownload.enabled = false;
			
			var request:URLRequest = new URLRequest(
				_installerDownloadURL + _installerFileName + _applicationExtension);
			
			navigateToURL(request, "_blank");
		}
		
		//----------------------------------
		//    creationCompleteHandler
		//----------------------------------
		
		private function creationCompleteHandler(event:FlexEvent):void
		{
			if (FlexGlobals.topLevelApplication.parameters)
			{
				var parameters:Object = FlexGlobals.topLevelApplication.parameters;
				
				if (parameters.fileNamePrefix)
					_fileNamePrefix = parameters.fileNamePrefix;
				
				if (parameters.installerConfigFile)
					_installerConfigFile = parameters.installerConfigFile;
				
				if (parameters.installerFileName)
					_installerFileName = parameters.installerFileName;
				
				if (parameters.installerDownloadURL)
					_installerDownloadURL = parameters.installerDownloadURL;
			}
			
			var platform:String = Capabilities.os.split(" ")[0];
			if (platform == PLATFORM_WIN)
				_applicationExtension = APPLICATION_EXTENSION_WIN;
			else if (platform == PLATFORM_MAC)
				_applicationExtension = APPLICATION_EXTENSION_MAC;
			else
				warnPlatformNotSupported();
			
			_internetUtil = InternetUtil.instance;
			
			_internetUtil.fetch(
				_installerDownloadURL + _installerConfigFile, 
				internetFetchResultHandler
			);
		}
		
		//----------------------------------
		//    internetFetchResultHandler
		//----------------------------------
		
		private function internetFetchResultHandler():void
		{
			var configXML:XML
			
			if (!_internetUtil.errorOccurred)
			{
				configXML = XML(_internetUtil.result);
				
				var fileName:String = configXML.files.file.(@name == ID_FILE_NODE).@file.toString();
				
				parseSDKVersionNumber(fileName);
			}
			else
			{
				Alert.show(INFO_ERROR_RETRIEVE_CONFIG, TITLE_ERROR_INTERNET);
			}
		}
		
		//----------------------------------
		//    parseSDKVersionNumber
		//----------------------------------
		
		private function parseSDKVersionNumber(fileName:String):void
		{
			_sdkVersionString = fileName.substr(_fileNamePrefix.length).split("-")[0];
			
			btnDownload.enabled = true;
		}
		
		//----------------------------------
		//    warnPlatformNotSupported
		//----------------------------------
		
		private function warnPlatformNotSupported():void
		{
			Alert.show(INFO_ERROR_OPERATING_SYSTEM, TITLE_ERROR_PLATFORM);
		}
		
	]]></fx:Script>
	
	<fx:Declarations /> 
	
	<s:states>
		<s:State name="normal" />
		<s:State name="disclaimerState" />
	</s:states>

	<s:Image id="imgBackground" source="{EmbeddedMedia.background}"
			 height="300" width="300" />
	
	<s:Image id="imgLogo" source="{EmbeddedMedia.logo}"
			 height="165" width="164"
			 top="5"
			 horizontalCenter="0" />
	
	<s:Image id="imgHeader" source="{EmbeddedMedia.header}"
			 height="37" width="106"
			 top="181"
			 left="75" />
	
	<s:Label text="{_sdkVersionString}"
			 fontFamily="openSansBold"
			 color="0x162545" fontSize="32"
			 trackingLeft="-1" trackingRight="-1"
			 width="137"
			 left="163" top="190" />
	
	<s:Label text="{INFO_FOR_IDE}"
			 fontFamily="openSansSemibold"
			 color="0x717C91" fontSize="13"
			 textAlign="center"
			 width="250"
			 left="25" top="227" />
	
	<s:Label text="{LABEL_DISCLAIMER}"
			 width="250"
			 left="25" top="245"
			 color="0x717C91"
			 fontFamily="openSansLightItalic" fontSize="11"
			 textAlign="center"
			 buttonMode="true"
			 click="{currentState = 'disclaimerState'}" />
	
	<s:Button id="btnDownload" label="{LABEL_DOWNLOAD}"
			  bottom="8"
			  enabled="false"
			  height="30"
			  horizontalCenter="0"
			  buttonMode="true"
			  useHandCursor="true"
			  click="btnDownload_clickHandler(event)"
			  styleName="mainBtnStyle"/>

	<s:Graphic visible.normal="false">    
		<s:Rect width="300" height="300">
			<s:fill>
				<s:SolidColor color="0x000000" alpha="0.5"/>
			</s:fill>
		</s:Rect>
	</s:Graphic>
	
	<s:Button id="btnBack" label="{LABEL_BACK}"
			  height="15"
			  right="15" top="10"
			  fontSize="10"
			  buttonMode="true"
			  click="{currentState = 'normal'}"
			  visible.normal="false"
			  styleName="genericBtnStyle"/>
	
	<s:TextArea id="disclaimerTxtArea"
				bottom="10" left="10" right="10" top="25"
				fontFamily="openSansSemibold"
				editable="false"
				textFlow="{TextFlowUtil.importFromXML(INFO_DISCLAIMER_XML)}"
				visible.normal="false" />
</s:Application>